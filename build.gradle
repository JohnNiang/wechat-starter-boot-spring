plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.lombok" version "3.1.4"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'me.johnniang.wechat'
archivesBaseName = 'wechat-starter-boot-spring'
// version = '0.0.2-SNAPSHOT'
version = '0.0.4'
sourceCompatibility = '1.8'


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    // Jackson lib
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.8'

    // Apache lib
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'
    implementation 'org.apache.httpcomponents:httpmime:4.5.7'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.apache.commons:commons-io:1.3.2'

    // Lombok
//    compileOnly 'org.projectlombok:lombok:1.18.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

//    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
}

jar {
    enabled = true
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'wechat-starter-boot-spring'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Wechat Starter Boot Spring'
                description = 'Spring Boot Starter for Wechat'
                url = 'https://github.com/JohnNiang/wechat-starter-boot-spring'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'johnniang'
                        name = 'johnniang'
                        email = 'johnniang@riseup.net'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/JohnNiang/wechat-starter-boot-spring.git'
                    developerConnection = 'scm:https://github.com/JohnNiang/wechat-starter-boot-spring.git'
                    url = 'https://github.com/JohnNiang/wechat-starter-boot-spring.git'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}